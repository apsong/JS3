#! /usr/bin/tclsh

set DIR $::env(PWD)
while { $DIR != "/" } {
    if {[file exists "$DIR/modules"]} {
        break
    }
    set DIR [file dirname $DIR]
    cd $DIR
}
if {![file exists "$DIR/modules"]} {
    puts stderr "Error: Invalid work directory. Unable to find modules."
    exit 1
}

proc incr0 {_var {step 1}} {
    upvar 1 $_var var
    if {[info exists var]} {
        incr var $step
    } else {
        set var $step
    }
}

cd $DIR/modules/DocumentService/temp

foreach JOB [glob -type f -nocomplain *JOB*] {
    set job [regsub -- {SUBJOB.*$} $JOB {}]
    set mtime [file mtime $JOB]
    if {![info exists last_job] || ($job > $last_job)} {
        set last_job $job
        set min_mtime $mtime
    } elseif {$min_mtime > $mtime} {
        set min_mtime $mtime
    }
}
if {![info exists min_mtime]} {
    puts stderr "Error: No pdf found in [pwd]"
    exit 2
}
#puts "First file: $JOBs($min_mtime)  [clock format $min_mtime -format {%Y-%m-%d %H:%M:%S}]"
set shift [expr {$min_mtime % 60}]

foreach dir [glob -nocomplain job$last_job*] {
    foreach pdf [glob -nocomplain $dir/*.pdf] {
        set mtime [expr {([file mtime $pdf] - $shift) / 60 * 60}]
        incr0 pdf_num($mtime)
        incr0 pdf_size($mtime) [file size $pdf]
        incr0 count
    }
}
if {![array exists pdf_num]} {
    return
}

set mtimes [lsort [array names pdf_num]]
set first [lindex $mtimes 0]
set last [lindex $mtimes end]

puts "TOTAL: $count pdf / [expr {($last - $first)/60+1}] minutes"
puts "=============================="

for {set t $first} {$t <= $last} {incr t 60} {
    set mtime_str [clock format [expr {$t + $shift}] -format {%Y-%m-%d %H:%M:%S}]
    set count [format %7d [expr {[info exists pdf_num($t)] ? $pdf_num($t) : 0}]]
    set size  [format %7d [expr {[info exists pdf_size($t)] ? $pdf_size($t)/1024 : 0}]]
    puts "$mtime_str  : $count (size: $size KB)"
}
